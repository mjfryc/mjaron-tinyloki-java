plugins {
    id 'java'
    id 'maven-publish'
    id "signing"
    id 'org.barfuin.gradle.jacocolog' version '3.1.0'
}

group 'io.github.mjfryc'
version '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    // https://docs.gradle.org/8.10.1/userguide/upgrading_version_8.html#manually_declaring_dependencies
    // If using JUnit Jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacocoTestReport {
    dependsOn test
    reports {
        // csv.enabled true // This API is deprecated: https://docs.gradle.org/7.1/dsl/org.gradle.api.reporting.Report.html#org.gradle.api.reporting.Report:enabled
        csv.required = true

        html.required = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

java {
    withSourcesJar()
    withJavadocJar()

    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

project.plugins.withType(MavenPublishPlugin).configureEach {
    PublishingExtension publishing = project.extensions.getByType(PublishingExtension.class)
    publishing.publications.withType(MavenPublication).configureEach { mavenPublication ->
        mavenPublication.pom {
            name = "${project.group}:${project.name}"
            description.set(name.toString())
            url = "https://github.com/mjfryc/mjaron-tinyloki-java"
            licenses {
                license {
                    name = "MIT License"
                    url = "https://raw.githubusercontent.com/mjfryc/mjaron-tinyloki-java/main/LICENSE"
                }
            }
            developers {
                developer {
                    id = "mjfryc"
                    name = "Michał Jaroń"
                    email = "m.jaron@protonmail.com"
                }
            }
            scm {
                connection = "scm:git:https://github.com/mjfryc/mjaron-tinyloki-java"
                developerConnection = "scm:git:git@github.com:mjfryc/mjaron-tinyloki-java.git"
                url = "https://github.com/mjfryc/mjaron-tinyloki-java"
            }
        }
    }
}

// Source: https://docs.github.com/en/actions/guides/publishing-java-packages-with-gradle
// Source: https://github.com/rwinch/gradle-publish-ossrh-sample
// Source: https://docs.github.com/en/authentication/managing-commit-signature-verification/generating-a-new-gpg-key
// Source: https://github.com/rwinch/gradle-publish-ossrh-sample
publishing {
    println("Publishing: Package version: $version")
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/mjfryc/mjaron-tinyloki-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

def hasSigningKey = System.getenv("GPG_PASSWORD") != null && System.getenv("GPG_PRIVATE_KEY") != null
if (hasSigningKey) {
    System.out.println("GPG key and password found. Signing maven publications.")
    doSign(project)
} else {
    System.out.println("GPG key or password not found. Signing is skipped.")
}

void doSign(Project project) {
    println("Signing publications. All entries:")
    publishing.publications.configureEach {
        println("    Publication: " + it.name)
    }

    project.signing {
        required { project.gradle.taskGraph.hasTask("required") }
        def signingKey = System.getenv("GPG_PRIVATE_KEY")
        def signingPassword = System.getenv("GPG_PASSWORD")
        useInMemoryPgpKeys(signingKey, signingPassword)
        //sign configurations.archives
        //sign publishing.publications.maven
        sign publishing.publications.gpr
        //sign project
    }
}
